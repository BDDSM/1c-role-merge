///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО Фобизнес ПРО
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

Перем URIПространстваИмен;
Перем ТипОбъект;
Перем ТипПраво;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ФайлРолиПриемникаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ФайлРолиПриемника = ВыбратьФайл();		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьФайлИходныхРолей(Команда)	
	ФайлыИсходныхРолей.Добавить(ВыбратьФайл());	
КонецПроцедуры

&НаКлиенте
Процедура Объединить(Команда)
	ОбъединитьРоли();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВыбратьФайл()
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.ПолноеИмяФайла 	   = "";
	ДиалогОткрытияФайла.Заголовок 		   = "Выберите роль, файл конфигурации";
	ДиалогОткрытияФайла.Фильтр 			   = "Роль, файл конфигурации (.xml)|Rights.xml";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Возврат ДиалогОткрытияФайла.ПолноеИмяФайла;
	Иначе
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ВыполнитьПослеОкончанияВыбораФайла", ЭтотОбъект), "Файл не выбран!");
	КонецЕсли;
	
КонецФункции	

&наКлиенте
Процедура ВыполнитьПослеОкончанияВыбораФайла(Параметры) Экспорт 	
	Оповестить("Обновить"); 	
КонецПроцедуры

&НаСервере
Процедура ОбъединитьРоли()
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ФайлРолиПриемника);
	РольПриемник = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
	
	ЧтениеXML.Закрыть();
	
	Для Каждого ФайлИсходнойРоли Из ФайлыИсходныхРолей Цикл
		
		ЧтениеXML.ОткрытьФайл(ФайлИсходнойРоли.Значение);	
		РольИсточник = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		ЧтениеXML.Закрыть();
		ДобавитьПрава(РольПриемник, РольИсточник);
		
	КонецЦикла;	
	
	ЗаписьXML = Новый ЗаписьXML;
	ПараметрыЗаписиXML = Новый ПараметрыЗаписиXML("UTF-8", "1.0", Истина, Ложь, Символы.Таб); 
	ЗаписьXML.ОткрытьФайл(ФайлРолиПриемника, ПараметрыЗаписиXML);
	ЗаписьXML.ЗаписатьОбъявлениеXML();	
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, РольПриемник);
	ЗаписьXML.Закрыть();
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьПрава(РольПриемник, Знач РольИсточник)
	
	Для Каждого ОбъектРолиИсточник Из РольИсточник.object Цикл
		ОбъектРолиПриемник = ПолучитьОбъектРолиПриемник(РольПриемник, ОбъектРолиИсточник);
		УстановитьПраваНаОбъект(ОбъектРолиПриемник, ОбъектРолиИсточник);
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Функция ПолучитьОбъектРолиПриемник(РольПриемник, Знач ОбъектРолиИсточник)
	
	ОбъектРолиПриемник = Неопределено;
	ИмяОбъекта 		   = ОбъектРолиИсточник.name;
	СвойствоОбъект 	   = РольПриемник.Свойства().Получить("object");
	
	Если СвойствоОбъект <> Неопределено Тогда // есть свойство
		
		Если РольПриемник.Свойства().Получить("object").ВерхняяГраница = -1 Тогда // это свойство список			
			Для Каждого ОбъектРолиКандидат Из РольПриемник.object Цикл
				Если ОбъектРолиКандидат.name = ИмяОбъекта Тогда
					ОбъектРолиПриемник = ОбъектРолиКандидат;
					Прервать;
				КонецЕсли;
			КонецЦикла;				
		Иначе // добавлен только 1 объект, те это еще не список
			Если РольПриемник.object.name = ИмяОбъекта Тогда
				ОбъектРолиПриемник = РольПриемник.object;
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ОбъектРолиПриемник = Неопределено Тогда
		ОбъектРолиПриемник = ФабрикаXDTO.Создать(ТипОбъект);
		ОбъектРолиПриемник.name = ИмяОбъекта;  
		РольПриемник.object.Добавить(ОбъектРолиПриемник);
	КонецЕсли;
	
	Возврат ОбъектРолиПриемник;
	
КонецФункции	

&НаСервере
Процедура УстановитьПраваНаОбъект(ОбъектРолиПриемник, Знач ОбъектРолиИсточник)
	
	ДобавляемыеПрава = Новый Массив;
	Для Каждого ПравоНаОбъектИсточник Из ОбъектРолиИсточник.right Цикл
		ИмяПрава = ПравоНаОбъектИсточник.name;
		ПравоНаОбъектПриемник = Неопределено;
		Если ОбъектРолиПриемник.Свойства().Получить("right").ВерхняяГраница = -1 Тогда
			Для Каждого ПравоНаОбъектКандидат Из ОбъектРолиПриемник.right Цикл
				Если ПравоНаОбъектКандидат.name = ИмяПрава Тогда
					ПравоНаОбъектПриемник = ПравоНаОбъектКандидат;
					Прервать;
				КонецЕсли;	
			КонецЦикла;
		Иначе
			Если ОбъектРолиПриемник.right.name = ИмяПрава Тогда
				ПравоНаОбъектПриемник = ОбъектРолиПриемник.right;
			КонецЕсли	
		КонецЕсли;	
		Если ПравоНаОбъектПриемник = Неопределено Тогда			
			ПравоНаОбъектПриемник = ФабрикаXDTO.Создать(ТипПраво);
			ПравоНаОбъектПриемник.name = ИмяПрава;
			ПравоНаОбъектПриемник.value = Ложь;			
			ДобавляемыеПрава.Добавить(ПравоНаОбъектПриемник);
		КонецЕсли;
		ПравоНаОбъектПриемник.value = ПравоНаОбъектПриемник.value ИЛИ ПравоНаОбъектИсточник.value;
		
		УстановитьОграниченияНаУровнеЗаписи(ПравоНаОбъектПриемник, ПравоНаОбъектИсточник);
		
	КонецЦикла;	
	
	Для Каждого ДобавляемоеПраво Из ДобавляемыеПрава Цикл
		ОбъектРолиПриемник.right.Добавить(ДобавляемоеПраво);	
	КонецЦикла;
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура УстановитьОграниченияНаУровнеЗаписи(ПравоНаОбъектПриемник, Знач ПравоНаОбъектИсточник)
	
	Для Каждого Ограничение Из ПравоНаОбъектИсточник.restrictionByCondition Цикл
		ПравоНаОбъектПриемник.restrictionByCondition.Добавить(Ограничение);     	
	КонецЦикла;
	
КонецПроцедуры					

#КонецОбласти

#Область Инициализация

URIПространстваИмен = "http://v8.1c.ru/8.2/roles";
ТипОбъект 			= ФабрикаXDTO.Тип(URIПространстваИмен, "ObjectType"); 
ТипПраво 			= ФабрикаXDTO.Тип(URIПространстваИмен, "RightType");

#КонецОбласти

